defmodule PryIn.PlugTest do
  use PryIn.Case
  use Phoenix.ConnTest
  alias PryIn.InteractionStore

  @endpoint PryIn.TestEndpoint

  setup _ do
    PryIn.TestEndpoint.start_link()
    :ok
  end

  test "generates a request" do
    get(build_conn(), "/test")

    [interaction] = InteractionStore.get_state().finished_interactions
    assert interaction.start_time
    assert interaction.duration
    assert interaction.type == :request
    assert interaction.pid == inspect(self())
  end

  describe "action and controller" do
    test "in phoenix apps" do
      get(build_conn(), "/test")

      [interaction] = InteractionStore.get_state().finished_interactions
      assert interaction.action == "test_action"
      assert interaction.controller == "PryIn.TestController"
    end
  end

  describe "request_id" do
    test "uses the request_id generated by plug if present" do
      request_id = "abcd-1234"
      Logger.metadata(request_id: request_id)
      get(build_conn(), "/test")

      [interaction] = InteractionStore.get_state().finished_interactions
      assert interaction.interaction_id == request_id
    end

    test "generates an interaction_id if none is in loggers metadata" do
      get(build_conn(), "/test")

      [interaction] = InteractionStore.get_state().finished_interactions
      assert interaction.interaction_id
    end
  end
end
